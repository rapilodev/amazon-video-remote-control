#!/usr/bin/perl
use warnings;
use strict;
use utf8;
$| = 1;
{

    package MovieRemoteControlServer;

    use HTTP::Server::Simple::CGI;
    use base qw(HTTP::Server::Simple::CGI);

    sub handle_request {
        my $self = shift;
        my $cgi  = shift;

        print "HTTP/1.0 200 OK\r\n";
        return unless ref $cgi;
        my $url = $cgi->url( -path_info => 1, -relative => 1 );

        return css()                                    if $url eq '/style.css';
        return javascript()                             if $url eq '/script.js';
        return send_key( scalar( $cgi->param('key') ) ) if $cgi->param('key');
        return main_page();
    }

    sub content_type {
        return qq{Content-type:} . shift . qq{\n\n};
    }

    sub respond {
        my ( $type, $content ) = @_;
        $content =~ s/\n\s+/\n/g;
        print content_type($type) . $content;
        return 1;
    }

    sub send_key {
        my $key = shift;

        return unless $key;
        return unless $key =~ /^(LEFT|RIGHT|UP|DOWN|SPACE)$/;
        my $device = ( glob("/dev/input/by-path/*event-kbd") )[0];
        die "no keyboard found at /dev/input/by-path" unless $device;
        system qq{evemu-event $device --type EV_KEY --code KEY_$key --value 1 --sync};
        system qq{evemu-event $device --type EV_KEY --code KEY_$key --value 0 --sync} or die $!;
        respond 'application/json', qq!{"key":"$key"}!;
    }

    sub javascript {
        respond 'text/javascript', q!
    function init(){
        document.body.requestFullscreen();
    }
    function call(key){
        fetch('/?key='+key);
        window.navigator.vibrate(50);
    }
    document.addEventListener('keyup', function (e) {
        if ((e.key=='ArrowLeft')  || (e.key=='MediaRewind'))      call ('LEFT');
        if ((e.key=='ArrowRight') || (e.key=='MediaFastForward')) call ('RIGHT');
        if ((e.key=='ArrowUp')    || (e.key=='AudioVolumeUp'))    call ('UP');
        if ((e.key=='ArrowDown')  || (e.key=='AudioVolumeDown'))  call ('DOWN');
        if ((e.key==' ')          || (e.key=='MediaPlayPause'))   call ('SPACE');
    });
    window.addEventListener('DOMContentLoaded', init, false);
    !;
    }

    sub css {
        respond 'text/css', q!
    *{
        border:0;
        padding:0;
    }
    .centered {
      position: fixed;
      top: 50%;
      left: 50%;
      transform: translate(-50%, -50%);
    }
    body{
        background:#111;
    }
    table{
        width:100vmin;
        height:100vmin;
    }
    td{
        width:25vmin;
        height:25vmin;
        text-align:center;
    }
    button:hover,button:focus{
        border: solid 0.5vmin solid #333;
        box-shadow:1vmin 1vmin 0.5vmin black, -1vmin 1vmin 0.5vmin black,inset 0 1vmin 1vmin #666;
        outline: none;
    }
    button{
        transition: all 0.5s;
        margin:2vmin;
        width:30vmin;
        height:30vmin;
        font-size:5vmin;
        cursor:pointer;
        background:#ccc;
        color:black;
        border-radius:30vmin;
        border: solid 1vmin #333;
        box-shadow:1vmin 1vmin 1vmin black, -1vmin 1vmin 1vmin black;
    }
    !;
    }

    sub main_page {
        respond 'text/html;charset:utf-8;', q%
    <!DOCTYPE html>
    <html>
        <head>
            <title>Movie Remote Control</title>
            <meta charset="utf8">
            <meta name="viewport" content="initial-scale=1, maximum-scale=1">
            <link rel="stylesheet" href="style.css">
            <script src="script.js"></script>
        </head>
        <body>
            <div class="centered">
                <table>
                <tr>
                    <td></td>
                    <td><button onclick="call('UP')">Vol. +</button></td>
                    <td></td>
                </tr>
                <tr>
                    <td><button onclick="call('LEFT')">&lt;&lt;</button></td>
                    <td><button onclick="call('SPACE')">&#x25b6; / ||</button></td>
                    <td><button onclick="call('RIGHT')">&gt;&gt;</button></td>
                </tr>
                <tr>
                    <td></td>
                    <td><button onclick="call('DOWN')">Vol. -</button></td>
                    <td></td>
                </tr>
                </table>
            </div>
        </body>
    </html>
    %;
    }

}    # end of package

MovieRemoteControlServer->new(8080)->run();
